buildscript {
  // Defined here so that we can use it in the buildscript block
  project.ext.roboVMVersion = '2.3.6'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    google()
  }

  dependencies {
    classpath "org.wisepersist:gwt-gradle-plugin:1.0.6"
    classpath "com.android.tools.build:gradle:3.1.3"
    classpath "com.mobidevelop.robovm:robovm-gradle-plugin:$roboVMVersion"
  }
}

allprojects {
  version = "1.0"

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  ext {
    appName = "playn-gradle-template"
    playnVersion = "2.1-SNAPSHOT"
    gwtVersion = "2.8.0"
  }
}

project(":assets") {
  apply plugin: "java"
}

project(":core") {
  apply plugin: "java"

  sourceCompatibility = 1.7

  dependencies {
    implementation "io.playn:playn-scene:$playnVersion"
  }
}

project(":java") {
  apply plugin: "java"

  sourceCompatibility = 1.7

  dependencies {
    implementation project(":core")
    implementation project(":assets")
    implementation "io.playn:playn-java-lwjgl:$playnVersion"
  }

  project.ext.mainClassName = "io.playn.template.java.TemplateGameJava"
}

project(":android") {
  apply plugin: "android"

  sourceCompatibility = 1.7

  dependencies {
    implementation project(":core")
    implementation "io.playn:playn-android:$playnVersion"
  }

  android {
    buildToolsVersion "28.0.0"
    compileSdkVersion 28
    defaultConfig {
      applicationId "io.playn.template"
      minSdkVersion 15
      targetSdkVersion 28
      versionCode 1
      versionName "1.0"
    }
  }
}

project(":robovm") {
  apply plugin: "java"
  apply plugin: "robovm"

  sourceCompatibility = 1.7

  dependencies {
    implementation project(":core")
    implementation "com.mobidevelop.robovm:robovm-rt:$roboVMVersion"
    implementation "com.mobidevelop.robovm:robovm-objc:$roboVMVersion"
    implementation "com.mobidevelop.robovm:robovm-cocoatouch:$roboVMVersion"
    implementation "io.playn:playn-robovm:$playnVersion"
  }

  ext {
    mainClassName = "io.playn.template.robovm.TemplateGameRoboVM"
  }
}

project(":html") {
  apply plugin: "gwt"
  apply plugin: "war"

  sourceCompatibility = 1.7

  dependencies {
    implementation project(":assets")
    implementation project(":core")
    implementation "io.playn:playn-html:$playnVersion"
    implementation "io.playn:playn-html:$playnVersion:sources"
    implementation "io.playn:playn-scene:$playnVersion:sources"
  }

  gwt {
    gwtVersion="$project.gwtVersion"
    maxHeapSize="1G"
    minHeapSize="512M"

    modules "io.playn.template.TemplateGame"
    devModules "io.playn.template.TemplateGame"
    // devModules "samskivert.testola.GdxDefinitionSuperdev"
    // project.webAppDirName = "webapp"

    compiler {
      strict = true
      disableCastChecking = true
    }
  }
}
